Module doc2

use standard

use sid

use seq.icmd

&section Core main function

The function main in the core module is the main entry point for the scgi server.

The function main takes two arguements. The first is the user and the second is encoded as application/x-www-form-urlencoded. The second expects five fields:sid, func, selected, lastseclected, and text. The actual names of the fields are ignored so order is very important. Sid is the session id, func is the function to run, selected is the id of the element the user has selected, and lastselected is the previous selection by the user. The text is the text the user entered.

The func name is mangled the Tau function and module name which forms a valid C identifier. The Tau function must be of the form:&{ block &keyword function myname(sid:session, selected:seq.word, last:seq.word, text:seq.seq.word)cmdresult &}

The main function decodes the arguments, checks authorization, Loads the library, calls the function, and coverts the result into HTML, adds a HTTP header and outputs the result.

&section Application design

Any application must perform certain actions. One must authenticate the user, check that the user is authorized to perform the operation,.access to persitant storage, display information to the user, and gather user input. Modern web applications are built on HTML and CSS for displaying information, javascript for user interaction, a framework for authentication and aurhtorization. Mastering these tools requires many years of study and experience. Furthermore the standards for these technologies have experience considerably change over the years.

Our goal is to distill the essence of applications and provide a simple set of tools that allow for rapid building of applications.

Authenticate of the user and authorization we pass off to a LDAP provider(or Apache basic authenticatiion). 

A screen consists of four parts:&{ block 1.List of comands which can be represented as buttons at top of screen.To execute a command the user presses the button.&br 2.Area to type text which can be represented as a text box at the top of the screen.&br 3.Marked up text to be displayed.&br 4.Selectable named items in the screen.Paragraphs or table rows could be the selectable objects.&br 5.a session id to identify the current state.&}

In javascript, user input is handled at the key stroke level. This is too detailed a level for most application. We use a higher level model.

The user interacts with a screen by optionally selecting an item or typing text and then the user executes one of the commands.The executing of a commands sends infomation to the server consisting of &{ block session id, &br the item selected, &br the previous item selected, &br and the text typed.&}

The text in the screen can be marked up with the following.

&keyword &br Start a new line

&keyword &em emphasis.&em example

&keyword &strong strong emphasis.&strong example

&keyword &keyword Keyword of program.&keyword example

&em &{ &keyword literal...&em &} &{ literal this is an example of how a literal is displayed &}

&em &{ &keyword block...&em &} Indented block

&em &{ &keyword noformat..&em &} block of unformated output

&em &{ &keyword keyword..&em &} block of keywords. &{ keyword An example block &}

&em &{ &keyword select &em name...&em &} A section of text that can be selected

&em &{ &keyword table <caption> <rows of table> &em &} Defines a table

&keyword &row..&keyword &cell..&keyword &cell..Defines a row of with three columns.Must be placed within table. The rows can be selected are are assign names from natural numbers in the order ther appear in the text.

The command on the screen consists of &{ block A sequence of words to be displayed on the command button.&br A name of a library. &br A name of a module in the library.&br A name of a function in the library. The function is the command.&br Whether the command requires a user selection(inputselect), some typed text(inputtext)or both(inputand)or neither(inputnone)&br A seq of words for the argument of the function.&}

One representation of a comand is a word sequence such as &{ literal"doit taudocs doc2 doit inputselect"&} where the first five words represent the first five and any remaining words represents the argument.

Below is a single function that implements an application that consists of a single screen with one button which re-displays the same screen.

Function repeat(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 cmdresult(s, [ icmd."repeat taudocs doc2 repeat inputnone"],"A very simple user interaction. Pressing the repeat button will return the same screen.")

The following two functions implement an application with two screens where the second screen's contents depends on what the user has selected.

Function doc2(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 cmdresult(s, [ icmd."doit taudocs doc2 doit inputselect"],"select a number and press doit to see the square"+"&{ select"+"01 one"+"&}"+"&{ select"+"02 two"+"&}"+"&{ select"+"03 three"+"&}"+"&{ table example table &row one &cell 1 &row two &cell 2 &row three &cell 3 &row four &cell 4 &}")

Function doit(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 let result = if length.arg = 0 
   then"Nothing was selected"
   else"The square of"+ arg +"is"+ print(toint(arg_1)* toint(arg_1))
  cmdresult(s, [ icmd."back taudocs doc2 doc2 inputnone"], result +"&br This selection:"+ arg +"&br The last selection:"+ lastpos)

Some application state must be associated with a session to build useful application. This can be done by defining a type to represent the state and then using the parameterized module state to associate it with the session. The state type that this module pairs the supplied type with the session. The pairing cannot be changed, so a new session must be created to represent the change in state.

In the example3 below we pair a real number with the session and double the value with each push of the button.

use state.real

use real

Function example3(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 let thisstate = tostate(s, 2.1)
  let newstate = changestate(thisstate, 2.0 * data.thisstate)
  cmdresult(session.newstate, [ icmd."double taudocs doc2 example3 inputnone"],"The current value ="+ print(data.newstate, 5))

Some data must exist beyond the life of a session to be useful. We can do that by using a persitant encoding. The following allows a user to save a seq of words. Once saved the data canot be changed.

type helloworld is record key:int, body:seq.word

use seq.helloworld

function =(a:helloworld, b:helloworld)boolean key.a = key.b

function hash(a:helloworld)int key.a

type mydata is Encoding helloworld

function print(a:helloworld)seq.word {"&br"+ print.key.a + body.a }

Function start(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 cmdresult(s, [ icmd."add taudocs doc2 add inputnone"], @(+, print,"", mapping.mydata))

Function add(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 let d = mapping.mydata 
  let discard = encode(helloworld(length.d + 1, b_1), mydata)
  cmdresult(s, [ icmd."add taudocs doc2 add inputnone", 
  icmd."save taudocs doc2 save inputnone"], @(+, print,"", mapping.mydata))

Function save(s:session, arg:seq.word, lastpos:seq.word, b:seq.seq.word)cmdresult 
 let discard = flush.mydata 
  cmdresult(s, [ icmd."add taudocs doc2 add inputnone"],"&keyword Saved!"+ @(+, print,"", mapping.mydata))

&section Some files that make up the tau enviornment

&keyword taumain.This is the main executable.If no argument is given it becomes a scgi server listing on port 8000.If a single argument, arg1 is given, then the library arg1 is compiled.If three arguments are given, the first argument is a library, the second a module, and the third and a function name with the signiture &keyword function arg1 seq.word.With three arguments, the library is compiled, and the function ran.When taumain is exectured, stdlib.dylib is loaded and if ran as server it also loads Qauth.dylib.

&{ keyword stdlib.dylib &} dynamic library that implements tau.

&{ keyword basic.dylib &} dynamic library that extends the basic tau implementation. It can be create with./tau basic.

&keyword tau A bash script that wraps taumain. It allows a shebang to be included as the first line of a tau source file so the textwangler editor run command will compile the library.

&{ keyword starttau.html &} A html file that will start tau environment.

&{ keyword myscript.js &} The javascript source file.Included in the document home of the Apache web server. This script implements two functions:&em cmd5 for selecting and &em cmd7 for running commands.

The web server must be configured so /tau is mapped to the scgi server implemented by taumain. We also assume that /tau is protected with basic authentication so that remote_user is defined.

This must be added to the apache conf file &{ block <Location"/tau"> &br AuthType Basic &br AuthName"Restricted Files"&br AuthBasicProvider file &br AuthUserFile"/etc/apache2/passwords"&br Require user david &br SetEnvIf Request_URI /tau proxy-scgi-pathinfo &br ProxyPass scgi://localhost:8000/ &br </Location> &}

&section Create tau from source


&em * To create taumain run &{ block cc tau.c taumain.c scgilib.c -o tau &} This will need tau.h and scglib.h

&em * To create stdlib &{ block cc -dynamiclib stdlib.c -o stdlib.dylib -undefined dynamic_lookup &}

&em * Each library source is in a subdrectory of the same name as the library. Go ahead and set up the source for each subdirectory for the libraries stdlib, basic, and testlib and move the source files into the subdirectories. Run the commands &{ block./taumain stdlib &br./taumain testlib &br./taumain basic &} This should end up producing new files:stdlib.dylib testlib.dylib basic.dylib

Now the scgi server can be started with ./taumain

Note: Spaces before periods and hyphens are removed in parsing text so it is hard to get the space before the dash in unix comands. The trick is to insert a no break space by using the option space on the Mac keyboard. Also there is a difference between a peroid followed by a space and a period followed by any other character. This allows one to distingish between the end of a sentence and a period before a file extension.

&section Using tau from the command line.

One can edit the source of libraries with just about any text editor. Here are some details how Textwangler can be used as the editor. A &em mylang.&em plist file is included for highlighting keywords. An unix script &em tau is included that can be placed in a fixed location like /usr/local/bin. Then if a subdirectory named helloworld is created with a file named helloword.ls with the contents &{ block #!/usr/local/bin/tau &br &br run helloworld hello &br &br Library helloworld uses stdlib export helloworld &br &br module helloworld &br &br Function hello seq.word {"helloworld"} &br &} then the Textwangler run command will compile the library, run the function named hello in the package helloword and open up a Internet browser to display"helloworld". If the run line is omitted the library will compile but no function will be ran. The function must have no arguments and return type is seq.text

From the command line &em tau &em helloworld will do the same thing as the run command as long is issued in the parent directory of the libraries.

&section Problems

Security needs a &em lot of work.session id not long enough.

should have copy and paste in editor to move blocks of text around

Should have visual representation of previous selected item.

A single Double quote within // // messes up web display of source

&section  Todo
 

Better llvm bitcode generation

authorization needs improvement.


Allow other binary operators to be defined.operator + op1 op2



Inner product example

/function xx(a:seq(int), b:seq(int), i:int)int a_i*b_i

/function innerproduct(a:seq(int), b:seq(int))int @(+, xx(a, b), 0, arithseq(length.a, 1, 1))
